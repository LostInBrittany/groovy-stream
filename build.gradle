buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
  }
}

apply plugin: 'groovy'
apply plugin: 'nexus'

group = 'com.bloidonia'
version = '0.5.1'

repositories {
  mavenCentral()
}

dependencies {
  compile     'org.codehaus.groovy:groovy-all:2.1.0'
  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
  testRuntime fileTree( dir:'build/libs/', includes:[ "groovy-stream-${version}.jar".toString() ] )
}

compileGroovy.options.compilerArgs << "-Xlint:unchecked" << "-Werror"

groovydoc {
  docTitle = "Groovy Stream: A Lazy Generator"
  header = docTitle
  footer = ''
  windowTitle = docTitle
  use = true
  link( 'http://download.oracle.com/javase/6/docs/api/', 'java.' )
  link('http://groovy.codehaus.org/api/',  'groovy.', 'org.codehaus.groovy.')
}

def metaServices = new File( 'src/main/groovy/META-INF/services' )

task generateModule << {
  metaServices.mkdirs()
  new File( metaServices, 'org.codehaus.groovy.runtime.ExtensionModule' ).withWriter { w ->
    w.writeLine 'moduleName=groovy-stream'
    w.writeLine "moduleVersion=$version"
    w.writeLine 'extensionClasses=groovy.stream.StreamExtension'
    w.writeLine 'staticExtensionClasses='
  }
}

compileGroovy.dependsOn( generateModule )
test.dependsOn( jar )

jar {
  from( metaServices ) {
    include 'org.codehaus.groovy.runtime.ExtensionModule' into( 'META-INF/services' )
  }
}

groovydoc {
  use             = true
  destinationDir  = new File( "${project.buildDir.path}/groovydoc", project.version )
  header = '<a href="https://github.com/timyates/groovy-common-extensions">groovy-common-extensions</a>'
  footer = null

  link 'http://download.oracle.com/javase/6/docs/api/',                 'java.'
  link 'http://groovy.codehaus.org/api/',                               'groovy.', 'org.codehaus.groovy.'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier 'javadoc'
  from groovydoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives groovydocJar
}

modifyPom {
  project {
    name 'Groovy Stream'
    description 'A collection of classes to give a fluent builder for Streams (Lazy Groovy Generators).'
    url 'http://timyates.github.com/groovy-stream/'
    inceptionYear '2012'

    scm {
      url 'https://github.com/timyates/groovy-stream'
      connection 'scm:https://github.com/timyates/groovy-stream.git'
      developerConnection 'scm:git://github.com/timyates/groovy-stream.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'tim_yates'
        name 'Tim Yates'
        email 'tim.yates@gmail.com'
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.4'
}